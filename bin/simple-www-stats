#!/usr/bin/env perl

use strict;
use warnings;
use File::Basename;
use Geo::IPfree;
use JSON::XS;

# HTTP request log parser
{
    package Parser;

    sub new {
        my ($proto) = @_;
        my $class = ref($proto) || $proto;
        my $self = {};
        bless ($self, $class);
        return $self;
    }

    sub Reset {
        my ($self) = @_;
        undef $self->{REQUEST};
        undef $self->{DATETIME};
        undef $self->{IP};
        undef $self->{HOST};
        undef $self->{CODE};
        undef $self->{BYTES};
        undef $self->{REFERRER};
        undef $self->{AGENT};
        undef $self->{TOTALTIME};
        undef $self->{CACHE};
        undef $self->{BACKEND};
    }

    sub _ResetValue {
        my ($self, $value) = @_;
        if (($self->{REQUEST} // '') eq $value) {
            undef $self->{REQUEST};
        }
        if (($self->{DATETIME} // '') eq $value) {
            undef $self->{DATETIME};
        }
        if (($self->{IP} // '') eq $value) {
            undef $self->{IP};
        }
        if (($self->{HOST} // '') eq $value) {
            undef $self->{HOST};
        }
        if (($self->{CODE} // '') eq $value) {
            undef $self->{CODE};
        }
        if (($self->{BYTES} // '') eq $value) {
            undef $self->{BYTES};
        }
        if (($self->{REFERRER} // '') eq $value) {
            undef $self->{REFERRER};
        }
        if (($self->{AGENT} // '') eq $value) {
            undef $self->{AGENT};
        }
        if (($self->{TOTALTIME} // '') eq $value) {
            undef $self->{TOTALTIME};
        }
        if (($self->{CACHE} // '') eq $value) {
            undef $self->{CACHE};
        }
        if (($self->{BACKEND} // '') eq $value) {
            undef $self->{BACKEND};
        }
    }

    sub IsInitialized {
        my ($self) = @_;
        return defined($self->{REQUEST}) &&
               defined($self->{DATETIME}) &&
               defined($self->{HOST}) &&
               defined($self->{CODE}) &&
               defined($self->{BYTES}) &&
               defined($self->{REFERRER}) &&
               defined($self->{AGENT});
    }

    sub _Init {
        my ($self, @parts) = @_;
        for my $i (0 .. $#parts) {
            my $part = $parts[$i];
            if (!defined($self->{REQUEST}) and $part =~ /^[A-Z]{3,10} \/.* HTTP\/1.[01]/) {
                $self->_ResetValue($i);
                $self->{REQUEST} = $i;
            } elsif (!defined($self->{DATETIME}) and $part =~ /^[0-3]?\d\/\w\w\w\/20\d\d:[0-2]\d:[0-5]\d:[0-5]\d /) {
                $self->_ResetValue($i);
                $self->{DATETIME} = $i;
            } elsif (!defined($self->{AGENT}) and $part =~ /Mozilla\/\d/i) {
                $self->_ResetValue($i);
                $self->{AGENT} = $i;
            } elsif (!defined($self->{IP}) and $part =~ /\d{1,3}\.\d{1,3}\.\d{1,3}\.\d{1,3}/) {
                $self->_ResetValue($i);
                $self->{IP} = $i;
            } elsif (!defined($self->{HOST}) and $part =~ /^([a-z0-9_-]+\.){2,}[a-z]{2,6}$/) {
                $self->_ResetValue($i);
                $self->{HOST} = $i;
            } elsif (!defined($self->{CODE}) and $part =~ /^[1-5]\d\d$/) {
                $self->_ResetValue($i);
                $self->{CODE} = $i;
            } elsif (!defined($self->{BYTES}) and $part =~ /^\d+$/) {
                $self->_ResetValue($i);
                $self->{BYTES} = $i;
            } elsif (!defined($self->{REFERRER}) and $part =~ /^https?:/) {
                $self->_ResetValue($i);
                $self->{REFERRER} = $i;
            } elsif (!defined($self->{TOTALTIME}) and $part =~ /^t(ime)?:/) {
                $self->_ResetValue($i);
                $self->{TOTALTIME} = $i;
            } elsif (!defined($self->{CACHE}) and $part =~ /^c(ache)?:/) {
                $self->_ResetValue($i);
                $self->{CACHE} = $i;
            } elsif (!defined($self->{BACKEND}) and $part =~ /^(b|backend|u|upstream):/) {
                $self->_ResetValue($i);
                $self->{BACKEND} = $i;
            }
        }
    }

    sub Parse {
        my ($self, $line) = @_;
        my @parts = $line =~ /"[^"]*"|\[[^\]]*\]|[^\s]+/g;
        foreach (@parts) {
            s/^"(.*)"$/$1/;
            s/^\[(.*)\]$/$1/;
        }
        if (!$self->IsInitialized()) {
            $self->_Init(@parts);
        }
        my $request = defined($self->{REQUEST}) ? $parts[$self->{REQUEST}] // "" : "";
        my ($method, $path, $version) = ($request =~ /^(\S+) (.*?) (HTTP\/.*)/);
        if (!defined($method)) {
            $method = '<Invalid>';
            $path = '';
            $version = '<Invalid>';
        }
        my $file = ($path =~ /(.*)(\?.*)/) ? $1 : $path;
        my $filetype = 'none';
        if ($file =~ m/\.([a-z0-9]+)$/i) {
            $filetype = lc($1);
        }
        my $datetime = defined($self->{DATETIME}) ? $parts[$self->{DATETIME}] // "" : "";
        my $ip = defined($self->{IP}) ? $parts[$self->{IP}] // "" : "";
        $ip =~ s/^(0:0:0:0:0|:)(:FFFF:|:0:|:)((\d{1,3}\.){3}\d{1,3})$/$3/i;
        my $host = defined($self->{HOST}) ? $parts[$self->{HOST}] // "" : "";
        my $code = defined($self->{CODE}) ? $parts[$self->{CODE}] // "" : "";
        my $bytes = defined($self->{BYTES}) ? int($parts[$self->{BYTES}]) // 0 : 0;
        my $referrer = defined($self->{REFERRER}) ? $parts[$self->{REFERRER}] // "-" : "-";
        my $agent = defined($self->{AGENT}) ? $parts[$self->{AGENT}] // "-" : "-";
        my $totaltime = defined($self->{TOTALTIME}) ? $parts[$self->{TOTALTIME}] // "0" : "0";
        $totaltime =~ s/^t(ime)?://;
        my $cache = defined($self->{CACHE}) ? $parts[$self->{CACHE}] // "-" : "-";
        $cache =~ s/^c(ache)?://;
        my $backend = defined($self->{BACKEND}) ? $parts[$self->{BACKEND}] // "-" : "-";
        $backend =~ s/^(b|backend|u|upstream)://;
        return (
            HTTP => $version,
            METHOD => $method,
            DATETIME => $datetime,
            IP => $ip,
            CODE => $code,
            BYTES => $bytes,
            KBYTES => 1 + ($bytes >> 10),
            HOST => $host,
            PATH => $path,
            FILE => $file,
            FILETYPE => $filetype,
            REFERRER => $referrer,
            AGENT => $agent,
            TOTALTIME => $totaltime,
            CACHE => $cache,
            BACKEND => $backend,
            BACKENDTIME => ($backend ne '-') ? 0.0 + $backend : 0.0
        );
    }
}

# Global variables
my @lines_skipped = ();
my $total = 0;
my $totaltime = 0.0;
my $totalkb = 1;
my @conf_errors = ();
my %stat_httpcode = ();
my %stat_httpversion = ();
my %stat_httpmethod = ();
my %stat_host = ();
my %stat_country = ();
my %stat_timeofday = ();
my %stat_timeofday_raw = ();
my %stat_browser = ();
my %stat_platform = ();
my %stat_file = ();
my %stat_filetype = ();
my %stat_source = ();
my %stat_redirect = ();
my %stat_errortype = ();
my %hist_prcstime_raw = ();
my %hist_prcstime = ();
my $parser = Parser->new;
my $geo = Geo::IPfree->new;
$geo->Faster;
$geo->{cache} = 1;

# Normalize HTTP browser & platform OS
sub normalize_browser_platform {
    my ($ref_data) = @_;
    my $agent = $$ref_data{'AGENT'};
    if ($agent =~ m#(Chrome|Firefox|MSIE|Opera)[/ ](\d+)#i) {
        $$ref_data{'BROWSER'} = $1;
        $$ref_data{'BROWSER_VERSION'} = $2;
    } elsif ($agent =~ /(Safari)/i or $agent =~ /(WebKit)/i) {
        $$ref_data{'BROWSER'} = $1;
        if ($agent =~ m#Version/(\d+)#i) {
            $$ref_data{'BROWSER_VERSION'} = $1;
        }
    } elsif ($agent =~ /Trident/i) {
        $$ref_data{'BROWSER'} = 'MSIE';
        if ($agent =~ m/rv:(\d+)/i) {
            $$ref_data{'BROWSER_VERSION'} = $1;
        }
    } elsif ($agent =~ /Bing|Facebook|Google|NING|Pinterest|Yahoo/i or
             $agent =~ /curl|Java|PubSub|Reeder|wget/i or
             $agent =~ /bot|crawler|fetch|proxy|search|spider|swarm/i or
             $agent =~ /archive|check|link|monitor|policy|subscribe|valid/i or
             $agent =~ /^Mozilla\/4\.0 \(compatible;\)$/ or
             $agent =~ /^-?$/) {
        $$ref_data{'BROWSER'} = 'Bot-like';
    } else {
        $$ref_data{'BROWSER'} = 'Other';
    }
    if ($agent =~ /Android/i) {
        $$ref_data{'PLATFORM'} = 'Android';
    } elsif ($agent =~ /iPhone|iPod|iPad|iOS/i) {
        $$ref_data{'PLATFORM'} = 'iOS';
    } elsif ($agent =~ /Mac/i) {
        $$ref_data{'PLATFORM'} = 'Mac';
    } elsif ($agent =~ /Linux/i) {
        $$ref_data{'PLATFORM'} = 'Linux';
    } elsif ($agent =~ /Win/i) {
        $$ref_data{'PLATFORM'} = 'Windows';
    } else {
        $$ref_data{'PLATFORM'} = 'Other';
    }
}

# Normalize stat counter values
sub normalize_stat_values {
    my ($ref_data) = @_;
    $$ref_data{'HOSTNAME'} = $$ref_data{'HOST'} || '<blank>';
    my $hour = $$ref_data{'DATETIME'};
    $hour =~ s/^.*?\d{4}:(\d{2}):.*/$1/;
    $$ref_data{'HOUR'} = $hour;
    $$ref_data{'COUNTRY'} = '<Unknown>';
    if ($$ref_data{'IP'} =~ /^[\d\.]+$/) {
        my ($ipcode, $ipcountry) = $geo->LookUp($$ref_data{'IP'});
        if ($ipcountry =~ /Reserved/) {
            $ipcountry = '<Unknown>';
        }
        $$ref_data{'COUNTRY'} = $ipcountry || $ipcode;
    } elsif ($$ref_data{'IP'} ne '') {
        $$ref_data{'COUNTRY'} = '<Unknown (IPv6)>';
    }
    $$ref_data{'SOURCE'} = 'File';
    if ($$ref_data{'BACKEND'} ne '-') {
        $$ref_data{'SOURCE'} = 'Backend';
    } elsif ($$ref_data{'CACHE'} ne '-') {
        $$ref_data{'SOURCE'} = 'Cache';
    }
    $$ref_data{'FILECATEGORY'} = 'Markup';
    if ($$ref_data{'FILETYPE'} =~ /^(gif|ico|jpe?g|png)$/) {
        $$ref_data{'FILECATEGORY'} = 'Images';
    } elsif ($$ref_data{'FILETYPE'} =~ /^(css|less|sass)$/) {
        $$ref_data{'FILECATEGORY'} = 'Styles';
    } elsif ($$ref_data{'FILETYPE'} =~ /^(js|coffee|dart)$/) {
        $$ref_data{'FILECATEGORY'} = 'Scripts';
    } elsif ($$ref_data{'FILETYPE'} =~ /^(rss|xml|json)$/) {
        $$ref_data{'FILECATEGORY'} = 'Data';
    }
}

# Normalize error message
sub normalize_error_msg {
    my ($respcode, $host, $url) = @_;
    if ($respcode eq '400') {
        return "$respcode bad request syntax";
    } elsif ($respcode eq '403' or $respcode eq '405') {
        return "$respcode bot probe (WebDAV)";
    } elsif ($respcode eq '408') {
        return "$respcode timeout waiting for request";
    } elsif ($respcode eq '499') {
        return "$respcode client closed connection";
    } elsif ($host eq '') {
        return "$respcode <blank host>";
    }
    for (my $i = 0; $i < $#conf_errors; $i++) {
        my $re = $conf_errors[$i][0];
        my $msg = $conf_errors[$i][1];
        if ($url =~ /$re/i) {
            return "$respcode $msg";
        }
    }
    return "$respcode $host$url";
}

# Processes stat counters for a key
sub process_counter {
    my ($ref, $key, $kbytes, $time) = @_;
    if (!defined($$ref{$key})) {
        $$ref{$key} = ();
        $$ref{$key}{'requests'} = 0;
        $$ref{$key}{'kbytes'} = 0;
        $$ref{$key}{'time'} = 0.0;
    }
    $$ref{$key}{'requests'}++;
    $$ref{$key}{'kbytes'} += $kbytes;
    $$ref{$key}{'time'} += $time;
}

# Processes an HTTP log file line
sub process_line {
    my ($line) = @_;
    my %data = $parser->Parse($line);
    if (!$parser->IsInitialized()) {
        push(@lines_skipped, $line);
        return;
    }
    normalize_browser_platform(\%data);
    normalize_stat_values(\%data);
    my $respcode = $data{'CODE'};
    my $kbytes = $data{'KBYTES'};
    my $time = $data{'BACKENDTIME'};
    $total++;
    $totalkb += $kbytes;
    $totaltime += $time;
    process_counter(\%stat_timeofday_raw, ''.(int($data{'HOUR'}) / 3), $kbytes, $time);
    process_counter(\%hist_prcstime_raw, $data{'BACKENDTIME'}, $kbytes, $time);
    process_counter(\%stat_httpcode, "HTTP $respcode", $kbytes, $time);
    process_counter(\%stat_httpversion, $data{'HTTP'} || '<blank>', $kbytes, $time);
    process_counter(\%stat_httpmethod, $data{'METHOD'}, $kbytes, $time);
    process_counter(\%stat_country, $data{'COUNTRY'}, $kbytes, $time);
    process_counter(\%stat_host, $data{'HOSTNAME'}, $kbytes, $time);
    process_counter(\%stat_filetype, $data{'FILECATEGORY'}, $kbytes, $time);
    process_counter(\%stat_source, $data{'SOURCE'}, $kbytes, $time);
    process_counter(\%stat_browser, $data{'BROWSER'}, $kbytes, $time);
    process_counter(\%stat_platform, $data{'PLATFORM'}, $kbytes, $time);
    if ($respcode =~ /^20[0-9]$/ || $respcode eq '304') {
        process_counter(\%stat_file, $data{'HOST'}.$data{'FILE'}, $kbytes, $time);
    } elsif ($respcode =~ /^30[0-9]$/) {
        process_counter(\%stat_redirect, $data{'HOST'}.$data{'FILE'}, $kbytes, $time);
    } else {
        my $key = normalize_error_msg($respcode, $data{'HOST'}, $data{'PATH'});
        process_counter(\%stat_errortype, $key, $kbytes, $time);
    }
}

# Processes an HTTP log file
sub process_file {
    my ($filename) = @_;
    if ($filename ne "-" and !(-f $filename and -r $filename)) {
        die "ERROR: cannot read file: $filename";
    }
    my $fh;
    if ($filename =~ /\.bz2$/) {
        open($fh, "-|", "bzcat $filename") or die $!;
    } elsif ($filename =~ /\.gz$/) {
        open($fh, "-|", "zcat $filename") or die $!;
    } elsif ($filename =~ /\.lzma$/) {
        open($fh, "-|", "lzcat $filename") or die $!;
    } elsif ($filename =~ /\.xz$/) {
        open($fh, "-|", "xzcat $filename") or die $!;
    } else {
        open($fh, "<$filename") or die $!;
    }
    $parser->Reset();
    @lines_skipped = ();
    while (<$fh>) {
        process_line($_);
    }
    if ($parser->IsInitialized()) {
        foreach (@lines_skipped) {
            process_line($_);
        }
    }
    close($fh);
}

# Processes the config file
sub process_config {
    my ($filename) = @_;
    my $fh;
    open($fh, "<$filename") or die $!;
    while (<$fh>) {
        s/^\s*(.*?)\s*$/$1/;
        if (m/^(#.*)?$/) {
            next;
        }
        my ($glob, $msg) = split(/\s+/, $_, 2);
        my $re = $glob;
        $re =~ s/\\/\\\\/g;
        $re =~ s/\./\\./g;
        $re =~ s/\+/\\+/g;
        $re =~ s/\|/\\|/g;
        $re =~ s/\^/\\^/g;
        $re =~ s/\$/\\\$/g;
        $re =~ s/\(/\\(/g;
        $re =~ s/\)/\\)/g;
        $re =~ s/\[/\\[/g;
        $re =~ s/\]/\\]/g;
        $re =~ s/\{/\\{/g;
        $re =~ s/\}/\\}/g;
        $re =~ s/\*/.*/g;
        $re =~ s/\?/./g;
        my @arr = ("^$re\$", $msg);
        push(@conf_errors, \@arr);
    }
    close($fh);
}

# Percent calculation (safe from div-by-zero)
sub percent {
    my ($value, $total) = @_;
    if ($value <= 0 or $total <= 0) {
        return 0;
    } else {
        return $value * 100.0 / $total;
    }
}

# Format a number of KB (data size)
sub format_kb {
    my ($kb) = @_;
    if ($kb < 1024) {
        return sprintf("%d KB", $kb);
    } elsif ($kb < 1024 * 1024) {
        return sprintf("%.2f MB", $kb / 1024);
    } else {
        return sprintf("%.2f GB", $kb / (1024 * 1024));
    }
}

# Format a number of seconds (time)
sub format_secs {
    my ($secs) = @_;
    if ($secs < 60) {
        return sprintf("%.1f sec", $secs);
    } elsif ($secs < 3600) {
        return sprintf("%.1f min", $secs / 60);
    } else {
        return sprintf("%.1f hrs", $secs / 3600);
    }
}

# Print a formatted histogram
sub print_histogram {
    my ($handle, $hashref, $width, $max, $sortby) = @_;
    $sortby = $sortby || 'requests';
    my @sorted;
    if ($sortby eq 'key') {
        @sorted = sort keys %$hashref;
    } else {
        @sorted = reverse sort {$$hashref{$a}{$sortby} <=> $$hashref{$b}{$sortby}} keys %$hashref;
    }
    my $fmt = "%7d (%4.1f%%) reqs %12s (%4.1f%%) %12s (%4.1f%%)";
    $fmt = sprintf("  %%-%ds %s\n", $width || 30, $fmt);
    if (defined($max) and @sorted > $max) {
        $#sorted = $max - 1;
    }
    foreach my $key (@sorted) {
        my $count = $$hashref{$key}{'requests'};
        my $kbytes = $$hashref{$key}{'kbytes'};
        my $time = $$hashref{$key}{'time'};
        printf $handle $fmt, $key.':', $count, percent($count, $total),
               format_kb($kbytes), percent($kbytes, $totalkb),
               format_secs($time), percent($time, $totaltime);
    }
}

# Read config file
my $config_file = "/etc/simple-www-stats.conf";
if (-f $config_file) {
    process_config($config_file);
} elsif (-f dirname($0) . "/..$config_file") {
    process_config(dirname($0) . "/..$config_file");
} else {
    die "ERROR: missing config file $config_file";
}

# Process command-line arguments
my $output_json;
my $output_text;
my @filenames;
while (my $arg = shift @ARGV) {
    if ($arg eq '--json') {
        $output_json = shift @ARGV;
    } elsif ($arg eq '--text') {
        $output_text = shift @ARGV;
    } else {
        push(@filenames, $arg);
        process_file($arg);
    }
}
if (!@filenames) {
    die "ERROR: no access log file specified";
}

# Format time-of-day keys
foreach my $key (sort keys %stat_timeofday_raw) {
    my $timekey = sprintf("%02d:00-%02d:59", int($key) * 3, int($key) * 3 + 2);
    $stat_timeofday{$timekey} = $stat_timeofday_raw{$key};
}

# Calculate processing time thresholds
my @limits = (0.500, 0.750, 0.900, 0.950, 0.975, 0.990, 0.995, 0.998, 0.999, 1.0);
my $nextlimit = shift(@limits);
my $count = 0;
foreach my $time (sort {$a <=> $b} keys %hist_prcstime_raw) {
    $count += $hist_prcstime_raw{$time}{'requests'};
    if ($count / $total >= $nextlimit) {
        $hist_prcstime{$nextlimit} = $time;
        $nextlimit = shift(@limits);
    }
}

# Print output
if (defined($output_json)) {
    my $fh;
    open($fh, '>'.$output_json) or die "ERROR: Couldn't write to ".$output_json;
    printf $fh "\{\n";
    my %json_total = ( 'count' => $total, 'kb' => $totalkb, 'size' => format_kb($totalkb));
    printf $fh qq#  "total": %s,\n#, encode_json(\%json_total);
    printf $fh qq#  "http_code": %s,\n#, encode_json(\%stat_httpcode);
    printf $fh qq#  "http_version": %s,\n#, encode_json(\%stat_httpversion);
    printf $fh qq#  "http_method": %s,\n#, encode_json(\%stat_httpmethod);
    printf $fh qq#  "hostname": %s,\n#, encode_json(\%stat_host);
    printf $fh qq#  "country": %s,\n#, encode_json(\%stat_country);
    printf $fh qq#  "browser": %s,\n#, encode_json(\%stat_browser);
    printf $fh qq#  "platform": %s,\n#, encode_json(\%stat_platform);
    printf $fh qq#  "file_type": %s,\n#, encode_json(\%stat_filetype);
    printf $fh qq#  "file_source": %s,\n#, encode_json(\%stat_source);
    printf $fh qq#  "time_of_day": %s,\n#, encode_json(\%stat_timeofday);
    printf $fh qq#  "process_time": %s,\n#, encode_json(\%hist_prcstime);
    #printf $fh qq#  "file": %s,\n#, encode_json(\%stat_file);
    printf $fh qq#  "redirect": %s,\n#, encode_json(\%stat_redirect);
    printf $fh qq#  "error_type": %s,\n#, encode_json(\%stat_errortype);
    printf $fh "\}\n";
    close($fh);
}
if (!defined($output_json) or defined($output_text)) {
    my $fh;
    if (defined($output_text)) {
        open($fh, '>'.$output_text) or die "ERROR: Couldn't write to ".$output_text;
    } else {
        $fh = *STDOUT;
    }
    print $fh "Top Requests:\n";
    print_histogram($fh, \%stat_file, 80, 20);
    print $fh "Top Downloads:\n";
    print_histogram($fh, \%stat_file, 80, 20, 'kbytes');
    print $fh "Top Processing Time:\n";
    print_histogram($fh, \%stat_file, 80, 20, 'time');
    print $fh "Top Redirects:\n";
    print_histogram($fh, \%stat_redirect, 80, 20);
    print $fh "Errors:\n";
    print_histogram($fh, \%stat_errortype, 80);
    print $fh "Hostnames:\n";
    print_histogram($fh, \%stat_host);
    print $fh "Data Sources:\n";
    print_histogram($fh, \%stat_source);
    print $fh "File Types:\n";
    print_histogram($fh, \%stat_filetype);
    print $fh "Request Methods:\n";
    print_histogram($fh, \%stat_httpmethod);
    print $fh "Request Versions:\n";
    print_histogram($fh, \%stat_httpversion);
    print $fh "Response Codes:\n";
    print_histogram($fh, \%stat_httpcode);
    print $fh "Countries:\n";
    print_histogram($fh, \%stat_country);
    print $fh "Browsers:\n";
    print_histogram($fh, \%stat_browser);
    print $fh "Platforms:\n";
    print_histogram($fh, \%stat_platform);
    print $fh "Time of Day Distribution:\n";
    print_histogram($fh, \%stat_timeofday, 30, 8, 'key');
    print $fh "Backend Time Distribution:\n";
    foreach my $key (sort keys %hist_prcstime) {
        printf $fh "  %5.1f%% within: %8s seconds\n", $key * 100.0, $hist_prcstime{$key};
    }
    printf $fh "Total:\n";
    printf $fh "  %7d requests\n", $total;
    printf $fh "  %10s downloaded data\n", format_kb($totalkb);
    printf $fh "  %11s backend time\n", format_secs($totaltime);
    close($fh);
}
